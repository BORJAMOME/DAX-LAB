# Proyecto: Control de Presupuestos

Este proyecto es como tener una lupa sobre tus presupuestos: te dice de un vistazo qu√© proyectos tiran del carro y cu√°les est√°n de relleno.

![Widget](../../docs/imagenes/Widget.png)

### Qu√© hace

#### 1. Gr√°fico de barras que habla por s√≠ solo

  - Muestra las ventas totales por departamento.

  - Puedes elegir si quieres ver el Top 1, Top 3 o Top 5 de proyectos en un segmentador.

  - Los proyectos que est√°n en ese Top se iluminan en morado intenso; el resto queda en un morado m√°s suave. As√≠, con un golpe de vista sabes qui√©n manda.

#### 2. Tarjetas que resumen la historia

  - Una tarjeta te da los datos globales del Top que hayas seleccionado.

  - Otra tarjeta te recuerda el total general, para que veas la proporci√≥n que representa el Top sobre el conjunto.

  - Nada de tablas kilom√©tricas ni cifras confusas: todo limpio y directo.

#### 3. Narrativa autom√°tica que no te deja pensar demasiado

  - Seg√∫n c√≥mo est√©n repartidos los presupuestos, la herramienta te suelta un mensaje claro:

    - Si un proyecto acapara demasiado, te avisa de riesgo de dependencia.

    - Si todo est√° muy repartido, te dice que hay alta dispersi√≥n.

    - Y si todo est√° equilibrado, confirma que los proyectos principales aportan un peso razonable.


### C√≥mo est√° hecho:

#### 1. Preparar la base de datos y medidas principales

Primero, necesitamos los datos de los proyectos y sus presupuestos. Aqu√≠ se cre√≥ la medida m√°s b√°sica:

Total Presupuesto
```
Total Presupuesto = SUM(Proyectos[Presupuesto (‚Ç¨)])
```

#### 2.  Crear el ranking de proyectos

Para saber qui√©nes son los m√°s importantes, necesitamos ordenarlos por presupuesto:

```
Ranking Presupuesto

Ranking Presupuesto = 
RANKX(
    ALL(Proyectos[Proyecto]),
    [Total Presupuesto],
    ,
    DESC
)
```

#### 3. Tabla desconectada para el segmentador

Para permitir que el usuario elija Top 1, 3 o 5, creamos una tabla desconectada:

```
Ranking Presupuestos = GENERATESERIES(1, 5, 1)
```

  - Genera una serie de n√∫meros del 1 al 5.

  - Esta tabla no tiene relaci√≥n con la tabla de proyectos, por eso se llama desconectada.


En el segmentador tambi√©n puedes mostrar un nombre m√°s amigable:

```
Nombre Parametro = "Top " & 'Ranking Presupuestos'[Ranking Presupuestos]
```

#### 4. Colorear las barras seg√∫n el Top N seleccionado

Ahora queremos que el Top N se vea m√°s destacado en el gr√°fico de barras.

```
Color Barra = 
VAR SeleccionarTopN = SELECTEDVALUE('Ranking Presupuestos'[Ranking Presupuestos])
RETURN
IF([Ranking Presupuesto] <= SeleccionarTopN, "#5b5cff99", "#5b5cff20")
```

```
Texto Color funciona de forma similar, pero para el color del texto sobre las barras:

Texto Color = 
VAR SeleccionarTopN = SELECTEDVALUE('Ranking Presupuestos'[Ranking Presupuestos])
RETURN
IF([Ranking Presupuesto] <= SeleccionarTopN, "#5b5cff", "#b5b5b5")
```

#### 5. Crear la narrativa autom√°tica del Top N

Esta es la parte que hace que el widget hable por s√≠ mismo: explica la concentraci√≥n de los proyectos de forma clara.

```
Narrativa TopN = 
VAR SeleccionarTopN = INT( SELECTEDVALUE('Ranking Presupuestos'[Ranking Presupuestos], 3) )
VAR TotalPresupuesto = [Total Presupuesto]
VAR SinDatos = ISBLANK(TotalPresupuesto) || TotalPresupuesto = 0

VAR TableTopN =
    TOPN(
        SeleccionarTopN,
        ALLSELECTED('Proyectos'[Proyecto]),
        [Total Presupuesto],
        DESC
    )

VAR TopNSales = SUMX(TableTopN, [Total Presupuesto])
VAR SalesPercentage = DIVIDE(TopNSales, TotalPresupuesto, BLANK())

VAR RangoInferior = 
    SWITCH( SeleccionarTopN,
        1, 0.25,
        3, 0.55,
        5, 0.70
    )

VAR RangoSuperior =
    SWITCH( SeleccionarTopN,
        1, 0.40,
        3, 0.70,
        5, 0.85
    )

VAR Diagnostico =
    SWITCH(
        TRUE(),
        SinDatos, "No hay datos suficientes.",
        ISBLANK(RangoInferior) || ISBLANK(RangoSuperior), "Rango no definido.",
        SalesPercentage < RangoInferior, 
            SWITCH( SeleccionarTopN,
                1, "El proyecto principal tiene una aportaci√≥n reducida. Alta dispersi√≥n.",
                3, "Los 3 mayores no alcanzan una masa cr√≠tica suficiente.",
                5, "Los 5 principales muestran baja representatividad."
            ),
        SalesPercentage > RangoSuperior,
            SWITCH( SeleccionarTopN,
                1, "Un √∫nico proyecto concentra un peso excesivo. Riesgo elevado.",
                3, "Alta concentraci√≥n en pocos proyectos. Dependencia significativa.",
                5, "Concentraci√≥n excesiva en los 5 principales."
            ),
        SWITCH( SeleccionarTopN,
            1, "El proyecto principal mantiene un nivel de concentraci√≥n adecuado.",
            3, "Los 3 mayores representan una concentraci√≥n equilibrada.",
            5, "Los 5 principales aportan un peso representativo y razonable."
        )
    )

RETURN
IF(
    SinDatos,
    "Top " & SeleccionarTopN & ": Datos insuficientes",
    "Top " & SeleccionarTopN & ": " &
    FORMAT( TopNSales / 1000, "#,0" ) & " mil ‚Ç¨" &
    " (" & FORMAT(SalesPercentage, "0.0%") & " del total)" &
    UNICHAR(10) & Diagnostico
)
```

- TOPN selecciona los proyectos m√°s grandes seg√∫n el Top N elegido.

- SalesPercentage calcula qu√© porcentaje del total representan.

- RangoInferior/RangoSuperior define si la concentraci√≥n es baja, equilibrada o alta.


Esto evita tener que interpretar n√∫meros complejos: el widget ya te dice qu√© pasa.



#### Combinar todo en el gr√°fico y las tarjetas

- El gr√°fico de barras usa Color Barra y Texto Color para diferenciar los proyectos seg√∫n el Top N.

- Una tarjeta din√°mica muestra los totales del Top N seleccionado (usando Narrativa TopN).

- Otra tarjeta muestra el total general para comparar r√°pidamente.

As√≠, en un vistazo, el usuario sabe:

- Qu√© proyectos destacan

- Qu√© proporci√≥n del total representan

- Si la distribuci√≥n es equilibrada o riesgosa



--- 

üìÇ Se incluye el archivo PBIX para que puedas probarlo, tocarlo y verlo en acci√≥n.


